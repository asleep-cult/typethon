@module:
    #[create_module]
    | !statement+

statement:
    | !simple_statements
    | !function_def
    | !class_def
    # | !for_statement
    | !if_statement
    | !while_statement

simple_statements:
    #[@prepend]
    | !simple_statement (";" !simple_statement)* ";"? NEWLINE

simple_statement:
    #[create_pass_statement]
    | "pass"
    #[create_break_statement]
    | "break"
    #[create_continue_statement]
    | "continue"
    #[create_return_statement]
    | "return" !expression?
    #[create_expr_statement]
    | !expressions
    | !type_assignment

complex_block:
    #[@flatten]
    | NEWLINE INDENT !statement+ DEDENT

block:
    | !complex_block
    | !simple_statements

decorators: ("@" !expression NEWLINE)+

function_def:
    #[create_function]
    | !decorators? "def" !IDENTIFIER "(" !function_parameters? ")" "->" !type_expression (":" !block)?

function_parameters:
    #[@prepend]
    | !function_parameter ("," !function_parameter)* ","?

function_parameter:
    #[create_function_parameter]
    | !IDENTIFIER ":" !type_expression ("=" !expression)?

class_def:
    #[create_class]
    | !decorators? "class" !IDENTIFIER ":" !block

# for_statement:
    # [create_for_statement]
    # | "for" !target "in" !expression ":" !block

if_statement:
    #[create_if_statement]
    | "if" !expression ":" !block !else_statement?

else_statement:
    #[create_elif_statement]
    | "elif" !expression ":" !block !else_statement?
    | "else" ":" !block

while_statement:
    #[create_while_statement]
    | "while" !expression ":" !block

expression:
    | !disjunction
    | !lambda

disjunction:
    #[create_disjunction]
    | !conjunction ("or" !conjunction)+
    | !conjunction

conjunction:
    #[create_conjunction]
    | !inversion ("and" !inversion)+
    | !inversion

inversion:
    #[create_inversion]
    | "not" !inversion
    | !comparison

comparison:
    #[create_comparison]
    | !bitwise_or !comparator+
    | !bitwise_or

comparator:
    #[create_comparator_one]
    | !"==" !bitwise_or
    #[create_comparator_one]
    | !"!=" !bitwise_or
    #[create_comparator_one]
    | !"<=" !bitwise_or
    #[create_comparator_one]
    | !"<" !bitwise_or
    #[create_comparator_one]
    | !">=" !bitwise_or
    #[create_comparator_one]
    | !">" !bitwise_or
    #[create_comparator_two]
    | !"not" !"in" !bitwise_or
    #[create_comparator_one]
    | !"in" !bitwise_or
    #[create_comparator_two]
    | !"is" !"not" !bitwise_or

bitwise_or:
    #[create_binary_operator]
    | !bitwise_or !"|" !bitwise_xor
    | !bitwise_xor

bitwise_xor:
    #[create_binary_operator]
    | !bitwise_xor !"^" !bitwise_and
    | !bitwise_and

bitwise_and:
    #[create_binary_operator]
    | !bitwise_and !"&" !shift_expr
    | !shift_expr

shift_expr:
    #[create_binary_operator]
    | !shift_expr !"<<" !sum
    #[create_binary_operator]
    | !shift_expr !">>" !sum
    | !sum

sum:
    #[create_binary_operator]
    | !sum !"+" !term
    #[create_binary_operator]
    | !sum !"-" !term
    | !term

term:
    #[create_binary_operator]
    | !term !"*" !factor
    #[create_binary_operator]
    | !term !"/" !factor
    #[create_binary_operator]
    | !term !"//" !factor
    #[create_binary_operator]
    | !term !"%" !factor
    #[create_binary_operator]
    | !term !"@" !factor
    | !factor

factor:
    #[create_unary_operator]
    | !"+" !factor
    #[create_unary_operator]
    | !"-" !factor
    #[create_unary_operator]
    | !"~" !factor
    | !power

power:
    #[create_binary_operator]
    | !primary !"**" !factor
    | !primary

primary:
    #[create_attribute]
    | !primary "." !IDENTIFIER
    # | !primary "(" !arguments? ")"
    #[create_subscript]
    | !primary "[" !slices "]"
    | !atom

slices:
    #[@prepend]
    | !slice ("," !slice)* ","?

slice:
    #[create_slice]
    | !expression ":" !expression (":" !expression)?
    | !expression

atom:
    #[create_constant]
    | !"True"
    #[create_constant]
    | !"False"
    #[create_constant]
    | !"..."
    #[create_name]
    | !IDENTIFIER
    #[create_number]
    | !NUMBER
    #[create_string]
    | !STRING+
    | !tuple_or_lambda
    | !group_or_lambda

expression_list:
    #[@prepend]
    | !expression ("," !expression)+ ","?
    #[@sequence]
    | !expression ","

expressions:
    #[create_tuple]
    | !expression_list
    | !expression

lambda:
    #[create_block_lambda]
    | "::" !complex_block "::"
    #[create_expression_lambda]
    | "::" !expression

tuple_or_lambda_parameters:
    #[create_tuple_or_lambda_parameters]
    | "(" !expression_list? ")"

tuple_or_lambda:
    | !tuple_or_lambda_parameters
    #[add_lambda_parameters]
    | !tuple_or_lambda_parameters !lambda

group_or_lambda_parameters:
    #[create_group_or_lambda_parameters]
    | "(" !expression ")"

group_or_lambda:
    | !group_or_lambda_parameters
    #[add_lambda_parameters]
    | !group_or_lambda_parameters !lambda

type_assignment:
    #[create_type_assignment]
    | "type" !IDENTIFIER "=" !type_expression

type_expression:
    | !type_atom

type_struct:
    #[create_struct_type]
    | "{" !type_struct_fields "}"

type_struct_fields:
    #[@prepend]
    | !type_struct_field ("," !type_struct_field)* ","?

type_struct_field:
    #[create_struct_field]
    | !IDENTIFIER ":" !type_expression

type_atom:
    | !type_struct
    #[create_type_parameter]
    | "'" !IDENTIFIER
    #[create_constant]
    | !"Self"
    #[create_name]
    | !IDENTIFIER
