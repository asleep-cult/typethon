@module:
    #[create_module]
    | !statement+

statement:
    | !simple_statements
    | !function_def
    | !class_def
    | !use_statement
    # | !for_statement
    | !if_statement
    | !while_statement
    | !sum_type_statement

simple_statements:
    #[@prepend]
    | !simple_statement (";" !simple_statement)* ";"? NEWLINE

simple_statement:
    #[create_break_statement]
    | "break"
    #[create_continue_statement]
    | "continue"
    #[create_return_statement]
    | "return" !expression?
    #[create_expr_statement]
    | !expression
    | !function_prototype
    | !type_declaration
    | !declaration_statement

complex_block:
    #[@flatten]
    | NEWLINE INDENT !statement+ DEDENT

block:
    | !complex_block
    | !simple_statements

declaration_statement:
    #[create_declaration]
    | "let" !IDENTIFIER (":" !type_expression)? ("=" !expression)?

decorators: ("@" !expression NEWLINE)+

function_prototype:
    #[create_function_prototype]
    | !decorators? "def" !IDENTIFIER "(" !function_parameters? ")" "->" !type_expression

function_def:
    #[add_function_body]
    | !function_prototype ":" !block

function_parameters:
    #[@prepend]
    | !function_parameter ("," !function_parameter)* ","?

function_parameter:
    #[create_function_parameter]
    | !IDENTIFIER ":" !type_expression ("=" !expression)?

class_def:
    #[create_class]
    | !decorators? "class" !IDENTIFIER ("(" !type_parameter_list ")")? ":" !block

use_statement:
    #[create_use_statement]
    | "use" !type_expression ":" !block
    #[create_use_for_statement]
    | "use" !type_expression "for" !type_expression ":" !block

# for_statement:
    # [create_for_statement]
    # | "for" !target "in" !expression ":" !block

if_statement:
    #[create_if_statement]
    | "if" !expression ":" !block !else_statement?

else_statement:
    #[create_elif_statement]
    | "elif" !expression ":" !block !else_statement?
    | "else" ":" !block

while_statement:
    #[create_while_statement]
    | "while" !expression ":" !block

expression:
    | !disjunction
    #[create_assignment]
    | !expression "=" !expression

disjunction:
    #[create_disjunction]
    | !conjunction ("or" !conjunction)+
    | !conjunction

conjunction:
    #[create_conjunction]
    | !inversion ("and" !inversion)+
    | !inversion

inversion:
    #[create_inversion]
    | "not" !inversion
    | !comparison

comparison:
    #[create_comparison]
    | !bitwise_or !comparator+
    | !bitwise_or

comparator:
    #[create_comparator_one]
    | !"==" !bitwise_or
    #[create_comparator_one]
    | !"!=" !bitwise_or
    #[create_comparator_one]
    | !"<=" !bitwise_or
    #[create_comparator_one]
    | !"<" !bitwise_or
    #[create_comparator_one]
    | !">=" !bitwise_or
    #[create_comparator_one]
    | !">" !bitwise_or
    #[create_comparator_two]
    | !"not" !"in" !bitwise_or
    #[create_comparator_one]
    | !"in" !bitwise_or
    #[create_comparator_two]
    | !"is" !"not" !bitwise_or
    #[create_comparator_one]
    | !"is" !bitwise_or

bitwise_or:
    #[create_binary_operator]
    | !bitwise_or !"|" !bitwise_xor
    | !bitwise_xor

bitwise_xor:
    #[create_binary_operator]
    | !bitwise_xor !"^" !bitwise_and
    | !bitwise_and

bitwise_and:
    #[create_binary_operator]
    | !bitwise_and !"&" !shift_expr
    | !shift_expr

shift_expr:
    #[create_binary_operator]
    | !shift_expr !"<<" !sum
    #[create_binary_operator]
    | !shift_expr !">>" !sum
    | !sum

sum:
    #[create_binary_operator]
    | !sum !"+" !term
    #[create_binary_operator]
    | !sum !"-" !term
    | !term

term:
    #[create_binary_operator]
    | !term !"*" !factor
    #[create_binary_operator]
    | !term !"/" !factor
    #[create_binary_operator]
    | !term !"//" !factor
    #[create_binary_operator]
    | !term !"%" !factor
    #[create_binary_operator]
    | !term !"@" !factor
    | !factor

factor:
    #[create_unary_operator]
    | !"+" !factor
    #[create_unary_operator]
    | !"-" !factor
    #[create_unary_operator]
    | !"~" !factor
    | !power

power:
    #[create_binary_operator]
    | !primary !"**" !factor
    | !primary

primary:
    #[create_attribute]
    | !primary "." !IDENTIFIER
    #[create_call]
    | !primary "(" !arguments? ")"
    #[create_subscript]
    | !primary "[" !slices "]"
    | !atom

slices:
    #[@prepend]
    | !slice ("," !slice)* ","?

slice:
    #[create_slice]
    | !expression ":" !expression (":" !expression)?
    | !expression

arguments:
    #[@prepend]
    | !expression ("," !expression)* ","?

atom:
    #[create_constant]
    | !"true"
    #[create_constant]
    | !"false"
    #[create_constant]
    | !"..."
    #[create_name]
    | !IDENTIFIER
    #[create_number]
    | !NUMBER
    #[create_string]
    | !STRING+
    | !tuple_or_lambda
    | !group_or_lambda

expression_list:
    #[@prepend]
    | !expression ("," !expression)+ ","?
    #[@sequence]
    | !expression ","

lambda_block:
    #[exit_lambda_block]
    | !complex_block

lambda:
    #[create_block_lambda]
    | "::" !lambda_block "::"
    #[create_expression_lambda]
    | "::" !expression

tuple_or_lambda_parameters:
    #[create_tuple_or_lambda_parameters]
    | "(" !expression_list? ")"

tuple_or_lambda:
    | !tuple_or_lambda_parameters
    #[add_lambda_parameters]
    | !tuple_or_lambda_parameters !lambda

group_or_lambda_parameters:
    #[create_group_or_lambda_parameters]
    | "(" !expression ")"

group_or_lambda:
    | !group_or_lambda_parameters
    #[add_lambda_parameters]
    | !group_or_lambda_parameters !lambda

type_declaration:
    #[create_type_declaration]
    | "type" !IDENTIFIER "=" !type_declaration_expression

type_declaration_expression:
    | !type_expression
    | !data_type

data_type:
    #[create_struct_type]
    | "{" !type_struct_fields "}"
    #[create_tuple_type]
    | "(" !type_expression_list? ")"

type_expression:
    | !type_primary

type_primary:
    #[create_type_call]
    | !type_primary "(" !type_expression_list? ")"
    | !type_atom

type_expression_list:
    #[@prepend]
    | !type_expression ("," !type_expression)* ","?

type_struct_fields:
    #[@prepend]
    | !type_struct_field ("," !type_struct_field)* ","?

type_struct_field:
    #[create_struct_field]
    | !IDENTIFIER ":" !type_expression

sum_type_statement:
    #[create_sum_type]
    | "type" !IDENTIFIER "=" NEWLINE INDENT ("|" !sum_type_field NEWLINE)+ DEDENT

sum_type_field:
    #[create_sum_field]
    | !IDENTIFIER !data_type?

type_parameter:
    #[create_type_parameter]
    | "'" !IDENTIFIER

type_parameter_list:
    #[@prepend]
    | !type_parameter ("," !type_parameter)* ","?

type_atom:
    | !type_parameter
    #[create_self_type]
    | "Self"
    #[create_name]
    | !IDENTIFIER
    | "(" !type_atom ")"
